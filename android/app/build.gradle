apply plugin: "com.android.application"

import com.android.build.OutputFile

buildscript {
    repositories {
        maven { url "https://maven.fabric.io/public" }
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath "io.fabric.tools:gradle:1.+"
    }
}

apply plugin: "io.fabric"

repositories {
    maven { url "https://maven.fabric.io/public" }
}


apply from: "../../node_modules/react-native/react.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "net.digisquare"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }
    signingConfigs {
        release {
            storeFile file(DIGISQUARE_RELEASE_STORE_FILE)
            storePassword DIGISQUARE_RELEASE_STORE_PASSWORD
            keyAlias DIGISQUARE_RELEASE_KEY_ALIAS
            keyPassword DIGISQUARE_RELEASE_KEY_PASSWORD
        }
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86"
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [
                manifestApplicationId: "net.digisquare",
                onesignal_app_id: DIGISQUARE_ONE_SIGNAL_APP_ID_DEV,
                onesignal_google_project_number: DIGISQUARE_ONE_SIGNAL_GOOGLE_PROJECT_NUMBER_DEV
            ]
        }
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
            manifestPlaceholders = [
                manifestApplicationId: "net.digisquare",
                onesignal_app_id: DIGISQUARE_ONE_SIGNAL_APP_ID_PROD,
                onesignal_google_project_number: DIGISQUARE_ONE_SIGNAL_GOOGLE_PROJECT_NUMBER_PROD
            ]
        }
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
}

dependencies {
    compile project(":react-native-vector-icons")
    compile project(":react-native-maps")
    compile fileTree(dir: "libs", include: ["*.jar"])
    compile "com.android.support:appcompat-v7:23.0.1"
    compile "com.facebook.react:react-native:+"  // From node_modules
    compile project(":react-native-fabric")
    compile("com.crashlytics.sdk.android:crashlytics:2.5.5@aar") {
        transitive = true;
    }
    compile project(":react-native-onesignal")
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
  from configurations.compile
  into "libs"
}
